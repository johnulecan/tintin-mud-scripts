#ACTION {Vulcan}{bow deep;#killall}{5}
#ACTION {[HW]  %0  Vulcan the Conjuror (spellbound)}{smile}{1}
#action {| [GOA] [coma] Vulcan the General of the Army}{smile}{1}
#action {[Hi] Vulcan the Conjuror (spellbound)}{smile}{1}
#ACTION {[High]    Vulcan the Conjuror (spellbound)}{smile}{1}
#ACTION {[Simyarin]}{#cr}{1}

#action {The high wizards are Beldin, Titleist, Vulcan, Astrodeath, Carrion and Auric.}{#cr}

#conf {SPEEDWALK}{on}
#conf {PACKET PATCH}{9.99}
#conf color on

#var prep {[<034>N<025>B<016>o<007>t<043>v<052>2<061>.<043>0<088>] }

#nop 1st login
#action {------------------------------ GENERAL INFO -------------------------------}
{
    #delay {2}
    {
        #system echo "----------- Start: `date` ---------- " >> nanny.simyarin.chaos.bot.log;
        #config log plain;
        #log {a}{nanny.simyarin.chaos.bot.log};
        s;e;2s;u;southeast;2e;southeast;e;southeast;southeast;4e;enter gate;23e4n5e7s2w2nw;withdraw all;wear all;e2s2e7nw;
        #var container sack;
        create party;
        party name my_partee;
        party;
        setchurch;
        tag sack;
        #math {x}{1d7200};
        #math {x}{$x+7200};
        #delay {$x}{gosleep};
        #alias whp #var whp;
        whp 90;
        #alias hbd #var hbdelay;
        hbd 2;
        #delay {$hbdelay}{myheartbeat};
    }
}

#var church 0
#alias setchurch {#var church 1;#nop #showme $prep Old Wood...}
#alias unsetchurch {#var church 0}

#action {The Old Wood <e w n s>.}{setchurch}
#action {You are in the Old Wood. It is a dark and mysterious place, where ancient}{setchurch}
#action {The rolling hills}{unsetchurch}
#action {The rolling hills, near the river}{unsetchurch}
#action {A dark room.}{unsetchurch}

#nop getting tox/full/etc for heal
#var initialxp 0;
#var initialgc 0;
#action {[xp:%1 gc:%2][t:%3% s:%4% f:%5% h:%6%]}
{
    #var xp %1;
    #var gc %2;
    #var tox %3;
    #var soak %4;
    #var full %5;
    #var heal %6;
    #if {$initialxp == 0}{#var initialxp %1};
    #if {$initialgc == 0}{#var initialgc %2};
    #if {$oldxp == 0}{#var oldxp %1};
    #if {$oldgc == 0}{#var oldgc %2};
}

#action {** HP: %1/%2   SP: %3/%4}
{
    #var hp %1;
    #var mhp %2;
    #var sp %3;
    #var msp %4;
}{1}

#nop
#nop HEARTBEAT
#nop

#var special 0;
#var mp 0;

#alias {myheartbeat}
{
    #if {($church == 1) && ($timetosleep == 1)}{
        e4s2en;sell all from $container;s2w3s2w2nw;drop all from $container;store all;e2s2e7n5w4s23w;enter gate;7w;enter gate;#kill;#alias myheartbeat l;
    };

    #math php {$hp * 100 / $mhp};
    #math psp {$sp * 100 / $msp};

    #math {fr}{1d100};
    #if {$fr == 1}{l};
    #if {($fr < 10) && ($church == 1)}{party};
    #nop #showme $prep [HB][C:$church][%:$php|$psp][HBD:$hbdelay][k:$iskilling][knr:$killnr($target)][S:$special][$fr];

    #var didsomething 0;

    #if {$sp > $hp}
    {
        #math {dif}{$sp - $hp};
    };
    #else
    {
        #math {dif}{$hp - $sp};
    };

    #if {($dif > 50) && ($sp > 0) && ($hp > 80) && ($church == 1) && ($didsomething == 0) && ($special == 0)}
    {
        4w4s3ed7s;southeast;2e;2e;southeast;2e;northeast;4ese;
        buy special;drink special;put glass in $container;drop glass;
        wn4w;southwest;2w;northwest;2w2w;northwest;7nu3w4n4e;
        #var special 1;
        #delay {120}{#var special 0};
        #var didsomething 1;
    };

    #math {shdiff}{$sp - $hp};
    #if {($church == 1) && ($shdiff > 15) && ($didsomething == 0)}
    {
        #math {mp}{$mp + 1};
        #if {$mp > 8}{#var mp 1};
        #showme $prep need to do something: $mp;
        #if {$mp == 1}{detect magic on star};
        #if {$mp == 3}{feel};
        #if {$mp == 5}{cast light};
        #var didsomething 1;
    };
    
    #nop healing checks;
    #nop #if {($church == 1) && ($didsomething == 0) && (($php < 91) || ($psp < 91))};
    #if {($church == 1) && (($php < 91) || ($psp < 91)) && ($gc > 200)}
    {
        healme;
    };

    #if {$php > 100}
    {
        #var;
    };


    #if {($church == 1) && ($didsomething == 0) && ($php > 90) && ($php < 101) && ($psp > 90) && ($psp < 101) && ($sp > 200)}
    {
        #var didsomething 1;
        #if {$iskilling == 0}
        {
            #if {($chaosbend_on == 1) && ($chaosbend_atkill == 1)}{
                #showme $prep chaosbended attempt, healing and going back to try kill;
                #var iskilling 1;
            };
            #else {
                #var chaosbend_atkill 0;
                pickkill;
            };
            #var ne 1;
            #if {$chaosmark_on == 1}{
                #var chaosmarked 0;
            }
        };
        wimpy $whp;gokill;grin evil;tar $target;
    };

    #math {human}{1d50};
    #if {($church == 1) && ($human < 2) && ($didsomething == 0)}
    {
        #var didsomething 1;
        #nop lets react more human...;
        #nop first step, lets goto green and idle some;
        #math greendelay {1d60};
        5s;l;#delay {$greendelay}{l;5n};
    };

    #delay {$hbdelay}{myheartbeat};

    #nop AT KILL!!!!;
    #if {($atkill == 1) && ($oktokill ==1 )}
    {
        #if {($sp > 20) && ($exhaust == 0) && ($didsomething == 0)}
        {
           #if {$chaosmarked == 0}{chaosmark $target};
           f;
	   #var spell f;
        };

        #if {($sp < 31)}
        {
            wimpy 210;
        };
    };
}

#alias cmon #var chaosmark_on 1
#alias cmoff #var chaosmark_on 0
cmoff

#alias cbon #var chaosbend_on 1
#alias cboff #var chaosbend_on 0
cbon

#action {You have now claimed}{#var chaosmarked 1}
#action {You have marked another monster in another room. Hence you can not use the spell here.}{#var chaosmarked 1}
#action {This monster is already targeted for destruction by Uruloki.}{#var chaosmarked 1}

#gag {my_partee %0}{1}

#var spell l
#action {Your mind is too exhausted.}
{
	#var exhaust 1;
	#delay {10}{#var exhaust 0;$spell}
};

#action {You are already concentrating on a spell!}{#var exhaust 1;#delay {4}{#var exhaust 0}};
#var exhaust 0;

#var iskilling 0
#var killnr 0

#action {You grin evilly.}
{
    #if {($iskilling == 1) && ($church == 0)}
    {
        #delay {2}
        {
            $goin;
            #var {atkill}{1};
            #if {($ne == 1)}
            {
                #var oktokill 0;
                shape $target;
            };
        };
    };
}

#action {You can sense the divine presence of mighty TURGILDI at this place, and fall to}{do_t_room}
#action {You can sense the faintest residue of mighty TURGILDI around}{do_t_room}
#action {TURGILDI has been here, and you think there still might be}{do_t_room}
#action {You are able to sense the presence of TURGILDI, maybe you}{do_t_room}
#action {You are able to feel the presence of TURGILDI and its}{do_t_room}
#alias do_t_room {
    tame;
    #var spell tame;
    #var oktokill 1;
    #var ne 0;
}

#action {You feel the hopelessness and wasteness of a place where none of the mighty god}{do_nog_room}
#action {You can sense the eternal struggle between the gods here and their fighting}{do_nog_room}
#action {The pattern of magic is particularily thin and}{do_nog_room}
#action {The waves are milling at different directions from here as}{do_nog_room}
#action {You can sense the weave of the waves strongly here and}{do_nog_room}
#action {You have found the brocade of the pattern, weave so heavy}{do_nog_room}
#alias do_nog_room {
    #var oktokill 1;
    #var ne 0;
}


#action {You are currently in a room where Coth rules,}{do_c_room}
#action {You can sense the divine presence of mighty COTH at this place, and fall to your}{do_c_room}
#action {You can sense the faintest residue of mighty COTH around here.}{do_c_room}
#action {COTH has been here, and you think there still might be a chance}{do_c_room}
#action {You are able to sense the presence of COTH, maybe you should}{do_c_room}
#action {You are able to feel the hot breath of COTH in your neck,}{do_c_room}
#var chaosbend_atkill 0;
#alias do_c_room {
    #if {($ne == 1) && ($atkill == 1)}
    {
        #showme {$prep COTH!!!};

	#if {$chaosbend_on == 1}{
            #if {$chaosbend_atkill == 0}{
                chaosbend;
                #var chaosbend_atkill 1;
            };
            #else {
                #var chaosbend_atkill 0;
            };
        };
        #delay {1}{
            #var ne 0;
            #var atkill 0;
            #var iskilling 0;
            #var oktokill 0;
            $goout;
            goback;
        }
    }
}


#action {%* is in undamaged shape.}
{
    #if {($ne == 1) && ($atkill == 1)}
    {
        earthwave;
	#var spell earthwave;
    };
}{3}

#action {You are currently in a room where Coth rules}{bot_do_return}
#action {That is not here.}{bot_do_return}
#action {There is no %0 here.}{bot_do_return}
#action {%1 is in %2 shape.}{bot_do_return}
#action {%0 (%1)}{bot_do_return}

#alias {bot_do_return} {
    #if {($ne == 1) && ($atkill == 1)}
    {
        #showme {$prep $target not here!};
        #var chaosbend_atkill 0;
        #var ne 0;
        #var atkill 0;
        #var iskilling 0;
        #var oktokill 0;
        $goout;
        goback;
    }
}

#var totalspnow 0;
#action {You cast Light and start to illuminate the room.}{@addsp{10}}
#action {You start to focus on 'Ostaryn Pallalar-Faeth'.}{@addsp{2};#var sp_to_add_next_fire 18}
#action {You start to focus on 'Evarelara Pallalar-Faeth'.}{@addsp{2};#var sp_to_add_next_fire 18}
#action {You start to focus on 'Ilasse Pallalar-Faeth'.}{@addsp{2};#var sp_to_add_next_fire 18}
#action {You start to focus on 'Ysara Pallalar-Faeth'.}{@addsp{5};#var sp_to_add_next_fire 20}
#action {When you feel that you are surrounded by nothing but icy, cold air, you part}{@addsp{$sp_to_add_next_fire}}
#action {As you weaves the pattern of the world changes and you}{@addsp{200}}
#action {'A Chaos Star' is magical.}{@addsp{10}}

#function {addsp}
{
    #math {totalspnow}{$totalspnow + %0};
    #math {totalsp}{$totalsp + %0};
    #showme {<030>** %0 total sp used.<088>};
    #showme {<030>** $totalspnow total sp used this login.<088>};
    #showme {<030>** $totalsp total sp used since we started counting.<088>};
    #class nanny.simyarin.chaos.bot.save write nanny.simyarin.chaos.bot.save;
    #return;
}

#action {Your legs run %0 with you!}
{
    #if {$atkill == 1}
    {
        break;break;
        #var gobackin smile;
        #if {"%0" == "east"}{#var gobackin west};
        #if {"%0" == "west"}{#var gobackin east};
        #if {"%0" == "south"}{#var gobackin north;hmm};
        #if {"%0" == "north"}{#var gobackin south};
        #if {"%0" == "northeast"}{#var gobackin southwest};
        #if {"%0" == "northwest"}{#var gobackin southeast};
        #if {"%0" == "southeast"}{#var gobackin northwest};
        #if {"%0" == "southwest"}{#var gobackin northeast};
        #if {"%0" == "down"}{#var gobackin up};
        #if {"%0" == "up"}{#var gobackin down};
        #delay {2}
        {
            ;;;
            $gobackin;
            $goout;
            goback;
        };
        #var atkill 0;
    }
}

#alias docorpse
{
    #var link 0;
    #if {$didcorpse == 0}
    {
        get coins from corpse;
        put all from corpse in $container;
        get all from corpse;
        put all from purse in $container;
        put all from pouch in $container;
        put purse in $container;
        put pouch in $container;
        put all from ground in $container;
        #nop get corpse;
        break;break;
        #var iskilling 0;
        #var {atkill}{0};
        $goout;
        goback;
        #nop drop corpse;
        e4s2en;sell all from $container;sell all;get glass from $container;put glass in bin;s2w4nw;
        get coinses from $container;
        #var didcorpse 1;
    }
}
#var didcorpse 0;


#action {There was no %0 here.}
{
    #if {($atkill == 1)}
    {
        #var atkill 0;
        docorpse;
    }
}

#action {There is no %0 here.}
{
    #if {($atkill == 1)}
    {
        #var atkill 0;
        docorpse;
    }
}

#CLASS {nanny.simyarin.chaos.bot.save} OPEN
#CLASS {nanny.simyarin.chaos.bot.save} READ nanny.simyarin.chaos.bot.save
#CLASS {nanny.simyarin.chaos.bot.save} CLOSE
#action {%0 died.}
{
    #if {$atkill == 1}
    {
        #var atkill 0;
        docorpse;
        #delay {14}
        {
            #math {gtmp}{$gc - $oldgc};
            #var oldgc $gc;
            #math {xtmp}{$xp - $oldxp};
            #var oldxp $xp;
            #math {gains[%0][knr]}{$gains[%0][knr] + 1};
            #math {gains[%0][xp]}{$gains[%0][xp] + $xtmp};
            #math {gains[%0][gc]}{$gains[%0][gc] + $gtmp};
            #class nanny.simyarin.chaos.bot.save write nanny.simyarin.chaos.bot.save
        };
    };
}{6}

#action {> %0 died.}
{
    #if {$atkill == 1}
    {
        #var atkill 0;
        docorpse;
        #delay {14}
        {
            #math {gtmp}{$gc - $oldgc};
            #var oldgc $gc;
            #math {xtmp}{$xp - $oldxp};
            #var oldxp $xp;
            #math {gains[%0][knr]}{$gains[%0][knr] + 1};
            #math {gains[%0][xp]}{$gains[%0][xp] + $xtmp};
            #math {gains[%0][gc]}{$gains[%0][gc] + $gtmp};
            #class nanny.simyarin.chaos.bot.save write nanny.simyarin.chaos.bot.save
        };
    };
}{4}

#action {You got %0 coins in total.}
{
    #math {coins}{$coins + %0};
    #class nanny.simyarin.chaos.bot.save write nanny.simyarin.chaos.bot.save
}

#action {You take %0 coins from %1}
{
    #math {coins}{$coins + %0};
    #class nanny.simyarin.chaos.bot.save write nanny.simyarin.chaos.bot.save
}

#alias {rip}
{
    #math {tgc}{$gc - $initialgc};
    #math {txp}{$xp - $initialxp};
    #var ttxp 0;
    #var ttgc 0;
    #var ttknr 0;

    #showme {+--------------------------+-------+-----------------------------+-----------------------------+};
    #echo {| %-25s| %+5s | %+12s (%+12s) | %+12s (%+12s) |}{R.I.P.}{Times}{Experience}{Average}{Gold}{Average};
    #showme {+--------------------------+-------+-----------------------------+-----------------------------+};
    #foreach {*gains[]} {mob}
    {
        #math {axp}{$gains[$mob][xp]/$gains[$mob][knr]};
        #math {agc}{$gains[$mob][gc]/$gains[$mob][knr]};
        #echo {| %-25s| %+5G | %+12G (%+12G) | %+12G (%+12G) |}{$mob}{$gains[$mob][knr]}{$gains[$mob][xp]}{$axp}{$gains[$mob][gc]}{$agc};
        #math {ttxp}{$gains[$mob][xp] + $ttxp};
        #math {ttgc}{$gains[$mob][gc] + $ttgc};
        #math {ttknr}{$gains[$mob][knr] + $ttknr};
    };
    #showme {+--------------------------+-------+-----------------------------+-----------------------------+};
    #math {axp}{$ttxp/$ttknr};
    #math {agc}{$ttgc/$ttknr};
    #echo {| %-25s| %+5G | %+12G (%+12G) | %+12G (%+12G) |}{Total}{$ttknr}{$ttxp}{$axp}{$ttgc}{$agc};
    #showme {+--------------------------+-------+-----------------------------+-----------------------------+};
    #echo {| %+9G coins gained (no expenses).}{$coins};
    #echo {| %+9G coins gained.}{$tgc};
    #echo {| %+9G experience gained}{$txp};
    #showme {+----------------------------------------------------------------------------------------------+};
}
#var {atkill}{0};

#alias {healme}
{
    #if {$tox < 101}
    {
        e4sene;buy firebreather;drink firebreather;put bottles in $container;drop bottle;wsen;sell all from $container;s2w4nw;
        #var didsomething 1;
    };
    #else
    {
        #if {$full < 50}
        {
            #math {fr}{1d8};
            #if {$fr == 1}{4w4sw3s;call eagle;2nwnwn2w;climb mountain;jump;2nd;s;buy 4;ne;southeast;swim;2se;enter boat;2w;northwest;7nu3w4n4e};
            #if {$fr == 2}{e4sen3w;enter portal 2;2e;buy 1;2w;enter portal;3esw4nw};
            #if {$fr == 3}{e4s4eu;sit;buy apple;2d4w4nw};
            #if {$fr == 4}{4w4s2w7sw;buy 2;e7n2e4n4e};
            #if {$fr == 5}{4w3se2s7e3nw;buy 5;e3s7w2nw3n4e};
            #if {$fr == 6}{4w3se3se;buy 4;eat cake;drop cake;w3nw3n4e};
            #if {$fr == 7}{4w5se5u;enter mirror;3w;enter mirror;es2w3s2wn;buy bread;s2e3n2enw;enter mirror;3e;enter mirror;5dw5n4e};
            #if {$fr == 8}{4w4s13ws;southwest;w2n;enter house;w;buy salad;e;out;2se;northeast;n13e4n4e};
            #var didsomething 1;
        };
        #else
        {
            #if {$soak < 70}
            {
                #math {fr}{1d6};
                #if {$fr == 1}{4w4sw3s;call eagle;2nwnwn2w;climb mountain;jump;2nd;s;buy 1;ne;southeast;swim;2se;enter boat;2w;northwest;7nu3w4n4e};
                #if {$fr == 2}{e4sen3w;enter portal 2;2e;buy 4;2w;enter portal;3esw4nw};
                #if {$fr == 3}{e4s4eu;sit;buy milk;2d4w4nw};
                #if {$fr == 4}{4w4s8w4n;northwest;northwest;n2w2nd2n;northeast;buy 1;southwest;2su2s2es;southeast;southeast;4s8e4n4e};
                #if {$fr == 5}{4w3se2s7e3nw;buy 3;e3s7w2nw3n4e};
                #if {$fr == 6}{4w4s13ws;southwest;w2n;enter house;w;buy orange juice;e;out;2se;northeast;n13e4n4e};
                #var didsomething 1;
            }
        }
    }
}

#gag {Shell: Finished.}

#alias pu put all from ground in $container
#alias sel sell all from $container
#alias gp get %0 from $container
#alias pune put all from corpses in $container
#alias ep exa $container

#var container sack

#action {Blackblade from beyond the spheres (evil).}{#nop}{1}
#gag {Com:}
#gag {Shell doing:}

#alias {sim}{4w4s23w;enter gate;7w;enter gate}
#alias {simb}{out;7e;enter gate;23e4n4e}
#alias {chaos}{4w4s23w;enter gate;4w;northwest;northwest;w;northwest;2w;northwest;d2nw}
#alias {chaosb}{e2su;southeast;2e;southeast;e;southeast;southeast;4e;enter gate;23e4n4e}

#alias gosleep #var timetosleep 1
#var timetosleep 0

#PROMPT {[xp%0][%1]}{[xp%0][%1]}
#read nanny.simyarin.chaos.bot.kills

#read nanny.simyarin.chaos.bot.secret
#EVENT {PROGRAM START}{#ses {NannyMUD Chaos}{130.236.254.159}{2000}}
#event {SESSION CONNECTED}{user_pass;#spl}

